plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.3.0'
    id 'org.springframework.boot' version '4.0.2'
    id 'io.spring.dependency-management' version '1.1.7'
}
ext {
    springCloudVersion = "2025.1.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'TossPayment'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-kafka'
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.springframework.cloud:spring-cloud-stream-test-binder'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'tools.jackson.module:jackson-module-kotlin'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-r2dbc'

    // Exposed R2DBC
    implementation 'org.jetbrains.exposed:exposed-core:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-r2dbc:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-kotlin-datetime:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-java-time:1.0.0'

    // MySQL R2DBC Driver
    implementation 'io.asyncer:r2dbc-mysql:1.3.1'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    // Kotlin Logging
    implementation 'io.github.oshai:kotlin-logging-jvm:7.0.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'io.projectreactor:reactor-test'

    // MockK
    testImplementation 'io.mockk:mockk:1.13.16'

    // Coroutines Test
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
    }
}
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'ExternalIntegration'
    }
}

tasks.register('integrationTest', Test) {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform {
        includeTags 'ExternalIntegration'
    }
}
