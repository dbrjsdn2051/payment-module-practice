plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.spring'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = "2025.1.0"
}

dependencies {
    // Web
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    // Kafka
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka'

    // R2DBC
    implementation 'org.springframework.boot:spring-boot-starter-r2dbc'

    // MySQL R2DBC Driver
    implementation 'io.asyncer:r2dbc-mysql:1.3.1'
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Exposed R2DBC
    implementation 'org.jetbrains.exposed:exposed-core:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-r2dbc:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-kotlin-datetime:1.0.0'
    implementation 'org.jetbrains.exposed:exposed-java-time:1.0.0'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'tools.jackson.module:jackson-module-kotlin'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-reactor'

    // Kotlin Logging
    implementation 'io.github.oshai:kotlin-logging-jvm:7.0.0'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'io.mockk:mockk:1.13.16'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
    }
}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}